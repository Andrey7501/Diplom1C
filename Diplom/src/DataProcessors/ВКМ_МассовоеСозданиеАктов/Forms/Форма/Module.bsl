
&НаКлиенте
Процедура ПериодДляУказанияМесяцаПриИзменении(Элемент)
	ПериодДляУказанияМесяцаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьОперацию(Команда)
	
	ДлительнаяОперация = ЗапуститьОперациюНаСервере();
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультат", ЭтотОбъект);
	ОповещениеОПрогрессе = Новый ОписаниеОповещения("ТекущийПрогресс", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = ОповещениеОПрогрессе;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаСервере
Функция ПроверитьЗаполнениеКонстант()
	
	ТекстУведомления = "";
	Если Не ЗначениеЗаполнено(Константы.ВКМ_НоменклатураАбонентскаяПлата.Получить()) Тогда
		ТекстУведомления = "Не заполнена константа Номенклатура Абонентская плата.";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Константы.ВКМ_НоменклатураРаботыСпециалиста.Получить()) Тогда
		Если ТекстУведомления = "" Тогда
			ТекстУведомления = "Не заполнена константа Номенклатура Работы специалиста.";
		Иначе
			НовыйТекстУведомления = "Не заполнена константа Номенклатура Работы специалиста.";
			МассивСтрок = Новый Массив;
			МассивСтрок.Добавить(ТекстУведомления);
			МассивСтрок.Добавить(НовыйТекстУведомления);
			ТекстУведомления = СтрСоединить(МассивСтрок, Символы.ПС);
		КонецЕсли;			
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстУведомления) Тогда
		ОбщегоНазначения.СообщитьПользователю(ТекстУведомления);
		Возврат ТекстУведомления;
	КонецЕсли;

КонецФункции 

&НаКлиенте
Процедура ОбработатьРезультат(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТекущийПрогресс(Прогресс, ДополнительныеПараметры) Экспорт
	
	Если Прогресс.Прогресс <> Неопределено Тогда
		Состояние("Создание актов реализаций...", Прогресс.Прогресс.Процент);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьОперациюНаСервере()
	
	Договоры = Новый Массив;
		
	Для Каждого Строка Из Объект.СписокДоговоровРеализации Цикл
		Если Строка.Реализация = Документы.РеализацияТоваровУслуг.ПустаяСсылка() Тогда
			ДанныеСтроки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Строка.Договор, "Ссылка, Организация, Владелец");
			Договор = Новый Структура;
			Договор.Вставить("Организация", ДанныеСтроки.Организация);
			Договор.Вставить("Контрагент", ДанныеСтроки.Владелец);
			Договор.Вставить("Ссылка", ДанныеСтроки.Ссылка);
			Договоры.Добавить(Договор);			
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения,
				"Обработки.ВКМ_МассовоеСозданиеАктов.СоздатьСписокНаСервере",
				Договоры, Объект.ПериодДляУказанияМесяца);
КонецФункции

&НаСервере
Процедура  ПериодДляУказанияМесяцаПриИзмененииНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Договор
		|ПОМЕСТИТЬ ВТ_Договоры
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	НЕ ДоговорыКонтрагентов.ПометкаУдаления
		|	И ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
		|	И ДоговорыКонтрагентов.ВКМ_СрокДействия >= &ОкончаниеПериода
		|	И ДоговорыКонтрагентов.ВКМ_Дата <= &НачалоПериода
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Договор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Договоры.Договор КАК Договор,
		|	РеализацияТоваровУслуг.Ссылка КАК Реализация
		|ПОМЕСТИТЬ ВТ_Реализации
		|ИЗ
		|	ВТ_Договоры КАК ВТ_Договоры
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|		ПО ВТ_Договоры.Договор.Ссылка = РеализацияТоваровУслуг.Договор.Ссылка
		|ГДЕ
		|	РеализацияТоваровУслуг.Дата >= &НачалоПериода
		|	И РеализацияТоваровУслуг.Дата <= &ОкончаниеПериода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Договоры.Договор КАК Договор,
		|	ВТ_Реализации.Реализация КАК Реализация
		|ИЗ
		|	ВТ_Договоры КАК ВТ_Договоры
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Реализации КАК ВТ_Реализации
		|		ПО ВТ_Договоры.Договор = ВТ_Реализации.Договор";
	
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Объект.ПериодДляУказанияМесяца));
	Запрос.УстановитьПараметр("ОкончаниеПериода", КонецМесяца(Объект.ПериодДляУказанияМесяца));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();
	
	Объект.СписокДоговоровРеализации.Очистить();
	
	Пока Выборка.Следующий() Цикл
		СтрокаРеализации = Объект.СписокДоговоровРеализации.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРеализации, Выборка);
	КонецЦикла;
	
КонецПроцедуры
