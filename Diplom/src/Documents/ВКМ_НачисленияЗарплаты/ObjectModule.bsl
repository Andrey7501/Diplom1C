

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	
    СформироватьДвижения();
	
	РассчитатьОклад();
	
	РассчитатьОтпуск();
	
	РассчитатьНДФЛ();
	
	РассчитатьВзаиморасчетыССотрудниками();
	
	
КонецПроцедуры


Процедура СформироватьДвижения()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_НачисленияЗарплатыОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ВКМ_НачисленияЗарплатыОсновныеНачисления.ВидРасчета КАК ВидРасчета,
		|	ВКМ_НачисленияЗарплатыОсновныеНачисления.ДатаНачала КАК ДатаНачала,
		|	ВКМ_НачисленияЗарплатыОсновныеНачисления.ДатаОкончания КАК ДатаОкончания,
		|	ВКМ_НачисленияЗарплатыОсновныеНачисления.ГрафикРаботы КАК ГрафикРаботы,
		|	МАКСИМУМ(ВКМ_УсловияОплатыСотрудников.Период) КАК Период
		|ПОМЕСТИТЬ ВТ_ДанныеПоОкладу
		|ИЗ
		|	Документ.ВКМ_НачисленияЗарплаты.ОсновныеНачисления КАК ВКМ_НачисленияЗарплатыОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников КАК ВКМ_УсловияОплатыСотрудников
		|		ПО ВКМ_НачисленияЗарплатыОсновныеНачисления.Сотрудник = ВКМ_УсловияОплатыСотрудников.Сотрудник
		|			И ВКМ_НачисленияЗарплатыОсновныеНачисления.ДатаНачала >= ВКМ_УсловияОплатыСотрудников.Период
		|ГДЕ
		|	ВКМ_НачисленияЗарплатыОсновныеНачисления.Ссылка = &Ссылка
		|	И ВКМ_НачисленияЗарплатыОсновныеНачисления.ВидРасчета = &Оклад
		|
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_НачисленияЗарплатыОсновныеНачисления.Сотрудник,
		|	ВКМ_НачисленияЗарплатыОсновныеНачисления.ВидРасчета,
		|	ВКМ_НачисленияЗарплатыОсновныеНачисления.ДатаНачала,
		|	ВКМ_НачисленияЗарплатыОсновныеНачисления.ДатаОкончания,
		|	ВКМ_НачисленияЗарплатыОсновныеНачисления.ГрафикРаботы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДанныеПоОкладу.Сотрудник КАК Сотрудник,
		|	ВТ_ДанныеПоОкладу.ВидРасчета КАК ВидРасчета,
		|	ВТ_ДанныеПоОкладу.ДатаНачала КАК ПериодДействияНачало,
		|	ВТ_ДанныеПоОкладу.ДатаОкончания КАК ПериодДействияКонец,
		|	ВТ_ДанныеПоОкладу.ГрафикРаботы КАК ГрафикРаботы,
		|	ВКМ_УсловияОплатыСотрудников.Оклад КАК Показатель,
		|	NULL КАК БазовыйПериодНачало,
		|	NULL КАК БазовыйПериодКонец
		|ИЗ
		|	РегистрСведений.ВКМ_УсловияОплатыСотрудников КАК ВКМ_УсловияОплатыСотрудников
		|		ПРАВОЕ СОЕДИНЕНИЕ ВТ_ДанныеПоОкладу КАК ВТ_ДанныеПоОкладу
		|		ПО (ВТ_ДанныеПоОкладу.Сотрудник = ВКМ_УсловияОплатыСотрудников.Сотрудник)
		|			И (ВТ_ДанныеПоОкладу.Период = ВКМ_УсловияОплатыСотрудников.Период)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВКМ_НачисленияЗарплатыОсновныеНачисления.Сотрудник,
		|	ВКМ_НачисленияЗарплатыОсновныеНачисления.ВидРасчета,
		|	ВКМ_НачисленияЗарплатыОсновныеНачисления.ДатаНачала,
		|	ВКМ_НачисленияЗарплатыОсновныеНачисления.ДатаОкончания,
		|	ВКМ_НачисленияЗарплатыОсновныеНачисления.ГрафикРаботы,
		|	NULL,
		|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВКМ_НачисленияЗарплатыОсновныеНачисления.ДатаНачала, МЕСЯЦ, -12), МЕСЯЦ),
		|	КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(ВКМ_НачисленияЗарплатыОсновныеНачисления.ДатаНачала, МЕСЯЦ, -1), МЕСЯЦ)
		|ИЗ
		|	Документ.ВКМ_НачисленияЗарплаты.ОсновныеНачисления КАК ВКМ_НачисленияЗарплатыОсновныеНачисления
		|ГДЕ
		|	ВКМ_НачисленияЗарплатыОсновныеНачисления.Ссылка = &Ссылка
		|	И ВКМ_НачисленияЗарплатыОсновныеНачисления.ВидРасчета = &Отпуск";
	
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("Отпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();

	// Движения по регистру ВКМ_ОсновныеНачисления
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ПериодДействияНачало = Выборка.ПериодДействияНачало;
		Движение.ПериодДействияОкончание = Выборка.ПериодДействияКонец;
		Движение.БазовыйПериодНачало = Выборка.БазовыйПериодНачало;
		Движение.БазовыйПериодКонец= Выборка.БазовыйПериодКонец;
		Движение.ВидРасчета = Выборка.ВидРасчета;
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.ГрафикРаботы = Выборка.ГрафикРаботы;
		Движение.Показатель = Выборка.Показатель;

	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать();
	
	// Движения по регистру ВКМ_ДополнительныеНачисления
	Для Каждого ТекСтрДопНачисления Из ДополнительныеНачисления Цикл
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВидРасчета = ТекСтрДопНачисления.ВидРасчета; 
        Движение.Сотрудник = ТекСтрДопНачисления.Сотрудник;
		Движение.Результат = ТекСтрДопНачисления.СуммаНачисления;
	КонецЦикла;
	
	//движения по регистру ВКМ_ВыполненныеСотрудникомРаботы
	Для Каждого ТекСтрокаДополнительныеНачисления Из ДополнительныеНачисления Цикл
		Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Сотрудник = ТекСтрокаДополнительныеНачисления.Сотрудник;
		Движение.СуммаКОплате = ТекСтрокаДополнительныеНачисления.СуммаНачисления;
		Движение.ЧасовКОплате = ТекСтрокаДополнительныеНачисления.НачисленоЧасов;
	КонецЦикла;

	Движения.ВКМ_ОсновныеНачисления.Записать();
	Движения.ВКМ_ДополнительныеНачисления.Записать();
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записать();

КонецПроцедуры

Процедура РассчитатьОклад()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия / 8, 0) КАК Факт,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия / 8, 0) КАК План
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ВидРасчета = &ВидРасчетаОклад) КАК ВКМ_ОсновныеНачисленияДанныеГрафика";
	
	Запрос.УстановитьПараметр("ВидРасчетаОклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки - 1];
		
		Движение.КоличествоДней = Выборка.Факт;
		
		Если Выборка.План <> 0 Тогда
			Движение.Результат = Движение.Показатель * Выборка.Факт / Выборка.План;
		КонецЕсли;
		
		Если Движение.Сторно Тогда
			Движение.Результат = -Движение.Результат;
			Движение.КоличествоДней = -Движение.КоличествоДней;
		КонецЕсли;
		
	КонецЦикла;
	
	РассчитатьНДФЛ();
	
	Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);

КонецПроцедуры 

Процедура РассчитатьОтпуск()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.РезультатБаза, 0) КАК РезультатБаза,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.КоличествоДней, 0) КАК ОтработаноДнейБаза,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия, 0) КАК Факт
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(
		|				&Измерения,
		|				&Измерения,
		|				,
		|				ВидРасчета = &Отпуск
		|					И Регистратор = &Ссылка) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления
		|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
		|				ВидРасчета = &Отпуск
		|					И Регистратор = &Ссылка) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
		|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки
		|ГДЕ
		|	ВКМ_ОсновныеНачисления.ВидРасчета = &Отпуск
		|	И ВКМ_ОсновныеНачисления.Регистратор = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Отпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);

    Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	Запрос.УстановитьПараметр("Измерения", Измерения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки -1];
		Движение.КоличествоДней = Выборка.Факт/8;
		Движение.Результат = Выборка.РезультатБаза / Выборка.ОтработаноДнейБаза * Движение.КоличествоДней;
		Если Движение.Сторно Тогда
			Движение.КоличествоДней = -Движение.КоличествоДней;
			Движение.Результат = -Движение.Результат;
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);
	
	
КонецПроцедуры

Процедура РассчитатьНДФЛ()
	
	НДФЛ = 13;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	СУММА(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.Результат) КАК Результат
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления
		|ГДЕ
		|	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.Регистратор = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.Сотрудник
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВКМ_ДополнительныеНачисленияБазаВКМ_ДополнительныеНачисления.Сотрудник,
		|	СУММА(ВКМ_ДополнительныеНачисленияБазаВКМ_ДополнительныеНачисления.Результат)
		|ИЗ
		|	РегистрРасчета.ВКМ_ДополнительныеНачисления.БазаВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисленияБазаВКМ_ДополнительныеНачисления
		|ГДЕ
		|	ВКМ_ДополнительныеНачисленияБазаВКМ_ДополнительныеНачисления.Регистратор = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_ДополнительныеНачисленияБазаВКМ_ДополнительныеНачисления.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
      Движение = Движения.ВКМ_Удержания.Добавить();
	  Движение.ПериодРегистрации = Дата;
	  Движение.Сотрудник = Выборка.Сотрудник;
	  Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
	  Движение.Результат = (Выборка.Результат * НДФЛ) / 100;
	  
	КонецЦикла;
	  
	  Движения.ВКМ_Удержания.Записать();
	  
КонецПроцедуры

Процедура РассчитатьВзаиморасчетыССотрудниками()
	
	Запрос =Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисления.Результат, 0) КАК Результат
		|ПОМЕСТИТЬ ВТ_Начисления
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|ГДЕ
		|	ВКМ_ОсновныеНачисления.Регистратор = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВКМ_ДополнительныеНачисления.Сотрудник,
		|	ЕСТЬNULL(ВКМ_ДополнительныеНачисления.Результат, 0)
		|ИЗ
		|	РегистрРасчета.ВКМ_ДополнительныеНачисления КАК ВКМ_ДополнительныеНачисления
		|ГДЕ
		|	ВКМ_ДополнительныеНачисления.Регистратор = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Начисления.Сотрудник КАК Сотрудник,
		|	СУММА(ВТ_Начисления.Результат) КАК Результат
		|ПОМЕСТИТЬ ВТ_Группировка
		|ИЗ
		|	ВТ_Начисления КАК ВТ_Начисления
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Начисления.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Группировка.Сотрудник КАК Сотрудник,
		|	ВТ_Группировка.Результат КАК Результат,
		|	СУММА(ВКМ_Удержания.Результат) КАК НДФЛ
		|ИЗ
		|	ВТ_Группировка КАК ВТ_Группировка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
		|		ПО ВТ_Группировка.Сотрудник = ВКМ_Удержания.Сотрудник
		|ГДЕ
		|	ВКМ_Удержания.Регистратор = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Группировка.Сотрудник,
		|	ВТ_Группировка.Результат";
		
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.Сумма = Выборка.Результат - Выборка.НДФЛ;
		
	КонецЦикла;
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();

КонецПроцедуры 
