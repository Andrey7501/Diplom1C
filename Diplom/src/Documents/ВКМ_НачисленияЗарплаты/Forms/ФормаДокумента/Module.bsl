
&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	НачалоМесяца = НачалоМесяца(Объект.Дата);
	КонецМесяца = КонецМесяца(Объект.Дата);
	
	Для каждого СтрНачисление из Объект.ОсновныеНачисления цикл
		
		Если СтрНачисление.ДатаНачала < НачалоМесяца ИЛИ СтрНачисление.ДатаОкончания > КонецМесяца тогда
			Отказ = Истина;
			ТекстСообщения = СтрШаблон("В строке %1 выбранный период не соответствует месяцу документа", 
			СтрНачисление.НомерСтроки);
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);	
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если ЗначениеЗаполнено(Объект.Подразделение) Тогда
		ЗаполнитьНаСервере();
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не заполнено Подразделение. Автоматическое заполнение невозможно!");
	КонецЕсли;
	
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Объект.ОсновныеНачисления.Очистить();
	Объект.ДополнительныеНачисления.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.Сотрудник КАК Сотрудник,
		|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.ЧасовКОплатеПриход КАК ЧасовКОплатеПриход,
		|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.СуммаКОплатеПриход КАК СуммаКОплатеПриход
		|ИЗ
		|	РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(&НачалоПериода, &КонецПериода, , ) КАК ВКМ_ВыполненныеСотрудникомРаботыОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВКМ_ФизическиеЛица КАК ВКМ_ФизическиеЛица
		|		ПО ВКМ_ВыполненныеСотрудникомРаботыОбороты.Сотрудник = ВКМ_ФизическиеЛица.Ссылка
		|ГДЕ
		|	ВКМ_ФизическиеЛица.Подразделение = &Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВКМ_ФизическиеЛица.Ссылка КАК Сотрудник,
		|	ВКМ_ФизическиеЛица.ГрафикРаботы КАК ГрафикРаботы
		|ИЗ
		|	Справочник.ВКМ_ФизическиеЛица КАК ВКМ_ФизическиеЛица
		|ГДЕ
		|	ВКМ_ФизическиеЛица.Подразделение = &Подразделение";
	
	НачалоМесяца = НачалоМесяца(НачалоДня(Объект.Дата));
	КонецМесяца = КонецМесяца(КонецДня(Объект.Дата));
	
	Запрос.УстановитьПараметр("Подразделение", Объект.Подразделение);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца);
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ВыборкаДополнительныеНачисления = МассивРезультатов[0].Выбрать();
	ВыборкаОсновныеНачисления = МассивРезультатов[1].Выбрать();
	
	Пока ВыборкаДополнительныеНачисления.Следующий() Цикл
		
		НовСтрДопНачисления = Объект.ДополнительныеНачисления.Добавить();
		НовСтрДопНачисления.Сотрудник = ВыборкаДополнительныеНачисления.Сотрудник;
		НовСтрДопНачисления.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.Процент;
		НовСтрДопНачисления.СуммаНачисления = ВыборкаДополнительныеНачисления.СуммаКОплатеПриход;
		НовСтрДопНачисления.НачисленоЧасов = ВыборкаДополнительныеНачисления.ЧасовКОплатеПриход;
		
	КонецЦикла;
	
	Пока ВыборкаОсновныеНачисления.Следующий() Цикл
		
		НовСтрОснНачисления = Объект.ОсновныеНачисления.Добавить();
		НовСтрОснНачисления.Сотрудник = ВыборкаОсновныеНачисления.Сотрудник;
		НовСтрОснНачисления.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад;
		НовСтрОснНачисления.ДатаНачала = НачалоМесяца;
		НовСтрОснНачисления.ДатаОкончания = КонецМесяца;
		НовСтрОснНачисления.ГрафикРаботы = ВыборкаОсновныеНачисления.ГрафикРаботы;
		
	КонецЦикла;
	
КонецПроцедуры
